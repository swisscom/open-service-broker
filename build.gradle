plugins {
    id 'groovy'
    id 'signing'
    id 'maven-publish'
    id 'nebula.info' version '5.0.0' apply false
    id 'nebula.maven-publish' version '10.0.0' apply false
    id 'nebula.javadoc-jar' version '10.0.0' apply false
    id 'nebula.source-jar' version '10.0.0' apply false
    id 'nebula.test-jar' version '10.0.0' apply false
    id 'nebula.nebula-bintray' version '7.0.0' apply false
    id 'org.springframework.boot' version '2.1.1.RELEASE' apply false
    id "com.github.jk1.dependency-license-report" version "1.2"
}

def brokerVersion = version
def isReleaseVersion = !version.endsWith('-SNAPSHOT')

ext.'signing.keyId' = project.signingKeyId
ext.'signing.password' = project.signingPassword
ext.'signing.secretKeyRingFile' = project.signingSecretKeyRingFile

allprojects {
    group = 'com.swisscom.cloud.sb'
    version = brokerVersion

    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

subprojects {
    apply plugin: 'groovy'

    signing {
        required { isReleaseVersion }
        sign publishing.publications
    }

    apply from: "$rootDir/gradle/repositories.gradle"
    apply from: "$rootDir/gradle/dependencies.gradle"
    apply from: "$rootDir/gradle/publishing.gradle"
}

licenseReport {
    renderer = new com.github.jk1.license.render.TextReportRenderer("THIRD-PARTY-NOTICES.txt")
}
